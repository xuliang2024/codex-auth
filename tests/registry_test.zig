const std = @import("std");
const builtin = @import("builtin");
const fs = @import("codex_auth").core.compat_fs;
const account_api = @import("codex_auth").api.account;
const registry = @import("codex_auth").registry;
const fixtures = @import("support/fixtures.zig");

fn b64url(allocator: std.mem.Allocator, input: []const u8) ![]u8 {
    const encoder = std.base64.url_safe_no_pad.Encoder;
    const out_len = encoder.calcSize(input.len);
    const buf = try allocator.alloc(u8, out_len);
    _ = encoder.encode(buf, input);
    return buf;
}

fn authJsonWithEmailPlan(allocator: std.mem.Allocator, email: []const u8, plan: []const u8) ![]u8 {
    const chatgpt_account_id = try chatgptAccountIdForEmailAlloc(allocator, email);
    defer allocator.free(chatgpt_account_id);
    const chatgpt_user_id = try chatgptUserIdForEmailAlloc(allocator, email);
    defer allocator.free(chatgpt_user_id);
    const header = "{\"alg\":\"none\",\"typ\":\"JWT\"}";
    const payload = try std.fmt.allocPrint(
        allocator,
        "{{\"email\":\"{s}\",\"https://api.openai.com/auth\":{{\"chatgpt_account_id\":\"{s}\",\"chatgpt_user_id\":\"{s}\",\"user_id\":\"{s}\",\"chatgpt_plan_type\":\"{s}\"}}}}",
        .{ email, chatgpt_account_id, chatgpt_user_id, chatgpt_user_id, plan },
    );
    defer allocator.free(payload);

    const h64 = try b64url(allocator, header);
    defer allocator.free(h64);
    const p64 = try b64url(allocator, payload);
    defer allocator.free(p64);

    const jwt = try std.mem.concat(allocator, u8, &[_][]const u8{ h64, ".", p64, ".sig" });
    defer allocator.free(jwt);

    return try std.fmt.allocPrint(allocator, "{{\"tokens\":{{\"account_id\":\"{s}\",\"id_token\":\"{s}\"}}}}", .{ chatgpt_account_id, jwt });
}

fn accountKeyForEmailAlloc(allocator: std.mem.Allocator, email: []const u8) ![]u8 {
    const chatgpt_user_id = try chatgptUserIdForEmailAlloc(allocator, email);
    defer allocator.free(chatgpt_user_id);
    const chatgpt_account_id = try chatgptAccountIdForEmailAlloc(allocator, email);
    defer allocator.free(chatgpt_account_id);
    return std.fmt.allocPrint(allocator, "{s}::{s}", .{ chatgpt_user_id, chatgpt_account_id });
}

fn hashPart(seed: u64, email: []const u8, modulus: u64) u64 {
    return std.hash.Wyhash.hash(seed, email) % modulus;
}

fn chatgptAccountIdForEmailAlloc(allocator: std.mem.Allocator, email: []const u8) ![]u8 {
    return std.fmt.allocPrint(
        allocator,
        "{d:0>8}-{d:0>4}-{d:0>4}-{d:0>4}-{d:0>12}",
        .{
            hashPart(1, email, 100_000_000),
            hashPart(2, email, 10_000),
            4000 + hashPart(3, email, 1000),
            8000 + hashPart(4, email, 1000),
            hashPart(5, email, 1_000_000_000_000),
        },
    );
}

fn chatgptUserIdForEmailAlloc(allocator: std.mem.Allocator, email: []const u8) ![]u8 {
    return std.fmt.allocPrint(
        allocator,
        "user-{x:0>8}{x:0>8}{x:0>6}",
        .{
            hashPart(6, email, 0x100000000),
            hashPart(7, email, 0x100000000),
            hashPart(8, email, 0x1000000),
        },
    );
}

fn legacySnapshotRelPath(allocator: std.mem.Allocator, email: []const u8) ![]u8 {
    const key = try b64url(allocator, email);
    defer allocator.free(key);
    const filename = try std.fmt.allocPrint(allocator, "{s}.auth.json", .{key});
    defer allocator.free(filename);
    return try fs.path.join(allocator, &[_][]const u8{ "accounts", filename });
}

fn makeEmptyRegistry() registry.Registry {
    return .{
        .schema_version = registry.current_schema_version,
        .active_account_key = null,
        .active_account_activated_at_ms = null,
        .api = registry.defaultApiConfig(),
        .accounts = std.ArrayList(registry.AccountRecord).empty,
    };
}

fn makeAccountRecord(
    allocator: std.mem.Allocator,
    email: []const u8,
    alias: []const u8,
    plan: ?registry.PlanType,
    auth_mode: ?registry.AuthMode,
    created_at: i64,
) !registry.AccountRecord {
    return .{
        .account_key = try accountKeyForEmailAlloc(allocator, email),
        .chatgpt_account_id = try chatgptAccountIdForEmailAlloc(allocator, email),
        .chatgpt_user_id = try chatgptUserIdForEmailAlloc(allocator, email),
        .email = try allocator.dupe(u8, email),
        .alias = try allocator.dupe(u8, alias),
        .account_name = null,
        .plan = plan,
        .auth_mode = auth_mode,
        .created_at = created_at,
        .last_used_at = null,
        .last_usage = null,
        .last_usage_at = null,
        .last_local_rollout = null,
    };
}

test "resolveCodexHomeFromEnv prefers CODEX_HOME over HOME" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    try tmp.dir.makePath("custom-codex");
    const custom_codex_home = try tmp.dir.realpathAlloc(gpa, "custom-codex");
    defer gpa.free(custom_codex_home);
    const resolved = try registry.resolveCodexHomeFromEnv(
        gpa,
        custom_codex_home,
        "/tmp/home-root",
        null,
    );
    defer gpa.free(resolved);

    try std.testing.expectEqualStrings(custom_codex_home, resolved);
}

test "resolveCodexHomeFromEnv rejects a missing CODEX_HOME override" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const missing = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(missing);
    const missing_path = try fs.path.join(gpa, &[_][]const u8{ missing, "missing-codex-home" });
    defer gpa.free(missing_path);

    try std.testing.expectError(
        error.FileNotFound,
        registry.resolveCodexHomeFromEnv(gpa, missing_path, "/tmp/home-root", null),
    );
}

test "resolveCodexHomeFromEnv rejects a file CODEX_HOME override" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    try tmp.dir.writeFile(.{ .sub_path = "codex-home.txt", .data = "not a directory" });
    const file_path = try tmp.dir.realpathAlloc(gpa, "codex-home.txt");
    defer gpa.free(file_path);

    try std.testing.expectError(
        error.NotDir,
        registry.resolveCodexHomeFromEnv(gpa, file_path, "/tmp/home-root", null),
    );
}

test "resolveCodexHomeFromEnv falls back to HOME when CODEX_HOME is empty" {
    const gpa = std.testing.allocator;

    const resolved = try registry.resolveCodexHomeFromEnv(
        gpa,
        "",
        "/tmp/home-root",
        null,
    );
    defer gpa.free(resolved);

    const expected = try fs.path.join(gpa, &[_][]const u8{ "/tmp/home-root", ".codex" });
    defer gpa.free(expected);

    try std.testing.expectEqualStrings(expected, resolved);
}

test "resolveCodexHomeFromEnv falls back to USERPROFILE when HOME is unset" {
    const gpa = std.testing.allocator;

    const resolved = try registry.resolveCodexHomeFromEnv(
        gpa,
        null,
        null,
        "C:\\Users\\demo",
    );
    defer gpa.free(resolved);

    const expected = try fs.path.join(gpa, &[_][]const u8{ "C:\\Users\\demo", ".codex" });
    defer gpa.free(expected);

    try std.testing.expectEqualStrings(expected, resolved);
}

fn setRecordIds(
    allocator: std.mem.Allocator,
    rec: *registry.AccountRecord,
    chatgpt_user_id: []const u8,
    chatgpt_account_id: []const u8,
) !void {
    allocator.free(rec.chatgpt_user_id);
    rec.chatgpt_user_id = try allocator.dupe(u8, chatgpt_user_id);
    allocator.free(rec.chatgpt_account_id);
    rec.chatgpt_account_id = try allocator.dupe(u8, chatgpt_account_id);
    allocator.free(rec.account_key);
    rec.account_key = try std.fmt.allocPrint(allocator, "{s}::{s}", .{ chatgpt_user_id, chatgpt_account_id });
}

fn countBackups(dir: fs.Dir, prefix: []const u8) !usize {
    var count: usize = 0;
    var it = dir.iterate();
    while (try it.next()) |entry| {
        if (entry.kind != .file) continue;
        if (std.mem.startsWith(u8, entry.name, prefix) and std.mem.containsAtLeast(u8, entry.name, 1, ".bak.")) {
            count += 1;
        }
    }
    return count;
}

fn expectBackupNameFormat(name: []const u8, prefix: []const u8) !void {
    const marker = ".bak.";
    try std.testing.expect(std.mem.startsWith(u8, name, prefix));
    const idx = std.mem.indexOf(u8, name, marker) orelse return error.TestExpectedEqual;
    const suffix = name[idx + marker.len ..];

    var stamp = suffix;
    if (std.mem.lastIndexOfScalar(u8, suffix, '.')) |dot_idx| {
        const maybe_counter = suffix[dot_idx + 1 ..];
        if (maybe_counter.len > 0) {
            for (maybe_counter) |ch| {
                if (!std.ascii.isDigit(ch)) return error.TestExpectedEqual;
            }
            stamp = suffix[0..dot_idx];
        }
    }

    if (stamp.len == 15 and stamp[8] == '-') {
        for (stamp, 0..) |ch, i| {
            if (i == 8) continue;
            try std.testing.expect(std.ascii.isDigit(ch));
        }
        return;
    }

    try std.testing.expect(stamp.len > 0);
    for (stamp) |ch| {
        try std.testing.expect(std.ascii.isDigit(ch));
    }
}

fn expectModeUnix(path: []const u8, expected_mode: u16) !void {
    if (comptime builtin.os.tag == .windows) return;
    const stat = try fs.cwd().statFile(path);
    try std.testing.expectEqual(@as(std.posix.mode_t, expected_mode), stat.permissions.toMode() & 0o777);
}

fn setModeUnix(path: []const u8, mode: u16) !void {
    if (comptime builtin.os.tag == .windows) return;
    try fs.cwd().inner.setFilePermissions(fs.io(), path, fs.File.Permissions.fromMode(mode), .{});
}

test "registry save/load" {
    var gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    const rec = try makeAccountRecord(gpa, "a@b.com", "work", .pro, .chatgpt, 1);
    try reg.accounts.append(gpa, rec);
    const active_account_key = try accountKeyForEmailAlloc(gpa, "a@b.com");
    defer gpa.free(active_account_key);
    try registry.setActiveAccountKey(gpa, &reg, active_account_key);
    reg.api.usage = true;
    try registry.setAccountLastLocalRollout(gpa, &reg.accounts.items[0], "/tmp/sessions/run-1/rollout-a.jsonl", 1735689600000);

    try registry.saveRegistry(gpa, codex_home, &reg);

    const registry_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "accounts", "registry.json" });
    defer gpa.free(registry_path);
    const saved = try fixtures.readFileAlloc(gpa, registry_path);
    defer gpa.free(saved);
    try std.testing.expect(std.mem.indexOf(u8, saved, "\"api\"") == null);
    try std.testing.expect(std.mem.indexOf(u8, saved, "\"previous_active_account_key\": null") != null);

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);
    try std.testing.expect(loaded.accounts.items.len == 1);
    try std.testing.expect(loaded.api.usage);
    try std.testing.expect(loaded.api.account);
    try std.testing.expect(loaded.active_account_activated_at_ms != null);
    try std.testing.expect(loaded.previous_active_account_key == null);
    try std.testing.expect(loaded.accounts.items[0].last_local_rollout != null);
    try std.testing.expectEqual(@as(i64, 1735689600000), loaded.accounts.items[0].last_local_rollout.?.event_timestamp_ms);
    try std.testing.expect(std.mem.eql(u8, loaded.accounts.items[0].last_local_rollout.?.path, "/tmp/sessions/run-1/rollout-a.jsonl"));
    try std.testing.expect(loaded.accounts.items[0].account_name == null);
}

test "registry save/load round-trips previous active account key" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "alpha@example.com", "alpha", .plus, .chatgpt, 1));
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "beta@example.com", "beta", .pro, .chatgpt, 2));

    const alpha_key = try accountKeyForEmailAlloc(gpa, "alpha@example.com");
    defer gpa.free(alpha_key);
    const beta_key = try accountKeyForEmailAlloc(gpa, "beta@example.com");
    defer gpa.free(beta_key);

    try registry.setActiveAccountKey(gpa, &reg, alpha_key);
    try registry.setActiveAccountKey(gpa, &reg, beta_key);
    try std.testing.expect(reg.previous_active_account_key != null);
    try std.testing.expectEqualStrings(alpha_key, reg.previous_active_account_key.?);

    try registry.saveRegistry(gpa, codex_home, &reg);
    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);

    try std.testing.expect(loaded.active_account_key != null);
    try std.testing.expect(loaded.previous_active_account_key != null);
    try std.testing.expectEqualStrings(beta_key, loaded.active_account_key.?);
    try std.testing.expectEqualStrings(alpha_key, loaded.previous_active_account_key.?);
}

test "setting same active account preserves previous active account key" {
    const gpa = std.testing.allocator;
    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "alpha@example.com", "alpha", .plus, .chatgpt, 1));
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "beta@example.com", "beta", .pro, .chatgpt, 2));

    const alpha_key = try accountKeyForEmailAlloc(gpa, "alpha@example.com");
    defer gpa.free(alpha_key);
    const beta_key = try accountKeyForEmailAlloc(gpa, "beta@example.com");
    defer gpa.free(beta_key);

    try registry.setActiveAccountKey(gpa, &reg, alpha_key);
    try registry.setActiveAccountKey(gpa, &reg, beta_key);
    try registry.setActiveAccountKey(gpa, &reg, beta_key);

    try std.testing.expect(reg.previous_active_account_key != null);
    try std.testing.expectEqualStrings(alpha_key, reg.previous_active_account_key.?);
}

test "plan labels are human-readable while registry stores raw plan values" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "label@example.com", "", .prolite, .chatgpt, 1));
    try registry.saveRegistry(gpa, codex_home, &reg);

    const registry_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "accounts", "registry.json" });
    defer gpa.free(registry_path);
    const saved = try fixtures.readFileAlloc(gpa, registry_path);
    defer gpa.free(saved);

    try std.testing.expect(std.mem.indexOf(u8, saved, "\"plan\": \"prolite\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, saved, "Pro Lite") == null);
    try std.testing.expectEqualStrings("Free", registry.planLabel(.free));
    try std.testing.expectEqualStrings("Plus", registry.planLabel(.plus));
    try std.testing.expectEqualStrings("Pro Lite", registry.planLabel(.prolite));
    try std.testing.expectEqualStrings("Business", registry.planLabel(.team));
}

test "resolveDisplayPlan prefers a usage snapshot plan over the stored auth plan" {
    const gpa = std.testing.allocator;
    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var rec = try makeAccountRecord(gpa, "display@example.com", "", .plus, .chatgpt, 1);
    rec.last_usage = .{
        .primary = null,
        .secondary = null,
        .credits = null,
        .plan_type = .team,
    };
    try reg.accounts.append(gpa, rec);

    try std.testing.expectEqual(registry.PlanType.plus, registry.resolvePlan(&reg.accounts.items[0]).?);
    try std.testing.expectEqual(registry.PlanType.team, registry.resolveDisplayPlan(&reg.accounts.items[0]).?);
}

test "registry load defaults missing account_name field to null" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");
    try tmp.dir.writeFile(.{
        .sub_path = "accounts/registry.json",
        .data =
        \\{
        \\  "schema_version": 3,
        \\  "active_account_key": null,
        \\  "accounts": [
        \\    {
        \\      "account_key": "user-ESYgcy2QkOGZc0NoxSlFCeVT::67fe2bbb-0de6-49a4-b2b3-d1df366d1faf",
        \\      "chatgpt_account_id": "67fe2bbb-0de6-49a4-b2b3-d1df366d1faf",
        \\      "chatgpt_user_id": "user-ESYgcy2QkOGZc0NoxSlFCeVT",
        \\      "email": "a@b.com",
        \\      "alias": "work",
        \\      "plan": "pro",
        \\      "auth_mode": "chatgpt",
        \\      "created_at": 1,
        \\      "last_used_at": null,
        \\      "last_usage_at": null
        \\    }
        \\  ]
        \\}
        ,
    });

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);

    try std.testing.expectEqual(@as(usize, 1), loaded.accounts.items.len);
    try std.testing.expect(loaded.accounts.items[0].account_name == null);
}

test "registry save/load round-trips account_name null" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    const rec = try makeAccountRecord(gpa, "a@b.com", "work", .pro, .chatgpt, 1);
    try reg.accounts.append(gpa, rec);
    try registry.saveRegistry(gpa, codex_home, &reg);

    const registry_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "accounts", "registry.json" });
    defer gpa.free(registry_path);
    const saved = try fixtures.readFileAlloc(gpa, registry_path);
    defer gpa.free(saved);
    try std.testing.expect(std.mem.indexOf(u8, saved, "\"account_name\": null") != null);

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);
    try std.testing.expect(loaded.accounts.items[0].account_name == null);
}

test "registry load normalizes schema four without previous active account key" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");
    try tmp.dir.writeFile(.{
        .sub_path = "accounts/registry.json",
        .data =
        \\{
        \\  "schema_version": 4,
        \\  "active_account_key": null,
        \\  "active_account_activated_at_ms": null,
        \\  "interval_seconds": 60,
        \\  "accounts": []
        \\}
        ,
    });

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);
    try std.testing.expect(loaded.previous_active_account_key == null);

    var file = try tmp.dir.openFile("accounts/registry.json", .{});
    defer file.close();
    const contents = try file.readToEndAlloc(gpa, 10 * 1024 * 1024);
    defer gpa.free(contents);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"schema_version\": 5") != null);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"previous_active_account_key\": null") != null);
}

test "registry save/load round-trips account_name string" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var rec = try makeAccountRecord(gpa, "a@b.com", "work", .pro, .chatgpt, 1);
    rec.account_name = try gpa.dupe(u8, "abcd");
    try reg.accounts.append(gpa, rec);
    try registry.saveRegistry(gpa, codex_home, &reg);

    const registry_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "accounts", "registry.json" });
    defer gpa.free(registry_path);
    const saved = try fixtures.readFileAlloc(gpa, registry_path);
    defer gpa.free(saved);
    try std.testing.expect(std.mem.indexOf(u8, saved, "\"account_name\": \"abcd\"") != null);

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);
    try std.testing.expect(loaded.accounts.items[0].account_name != null);
    try std.testing.expect(std.mem.eql(u8, loaded.accounts.items[0].account_name.?, "abcd"));
}

test "applyAccountNamesForUser preserves existing account_name when replacement allocation fails" {
    const gpa = std.testing.allocator;
    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var rec = try makeAccountRecord(gpa, "a@b.com", "work", .pro, .chatgpt, 1);
    rec.account_name = try gpa.dupe(u8, "Primary Workspace");
    try reg.accounts.append(gpa, rec);

    var entry = account_api.AccountEntry{
        .account_id = try gpa.dupe(u8, reg.accounts.items[0].chatgpt_account_id),
        .account_name = try gpa.dupe(u8, "Ops Workspace"),
    };
    defer entry.deinit(gpa);

    var failing_allocator = std.testing.FailingAllocator.init(gpa, .{ .fail_index = 0 });
    const entries = [_]account_api.AccountEntry{entry};

    try std.testing.expectError(
        error.OutOfMemory,
        registry.applyAccountNamesForUser(
            failing_allocator.allocator(),
            &reg,
            reg.accounts.items[0].chatgpt_user_id,
            &entries,
        ),
    );
    try std.testing.expect(reg.accounts.items[0].account_name != null);
    try std.testing.expectEqualStrings("Primary Workspace", reg.accounts.items[0].account_name.?);
}

test "applyAccountNamesForUser updates same-user records across personal and team workspaces" {
    const gpa = std.testing.allocator;
    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var team = try makeAccountRecord(gpa, "same@example.com", "", .team, .chatgpt, 1);
    try setRecordIds(gpa, &team, "user-shared", "acct-team");
    team.account_name = try gpa.dupe(u8, "Legacy Workspace");
    try reg.accounts.append(gpa, team);

    var plus = try makeAccountRecord(gpa, "same@example.com", "", .plus, .chatgpt, 2);
    try setRecordIds(gpa, &plus, "user-shared", "acct-plus");
    try reg.accounts.append(gpa, plus);

    var other = try makeAccountRecord(gpa, "other@example.com", "", .team, .chatgpt, 3);
    try setRecordIds(gpa, &other, "user-other", "acct-other");
    other.account_name = try gpa.dupe(u8, "Unrelated Workspace");
    try reg.accounts.append(gpa, other);

    var entry = account_api.AccountEntry{
        .account_id = try gpa.dupe(u8, "acct-team"),
        .account_name = try gpa.dupe(u8, "Primary Workspace"),
    };
    defer entry.deinit(gpa);

    const entries = [_]account_api.AccountEntry{entry};
    const changed = try registry.applyAccountNamesForUser(gpa, &reg, "user-shared", &entries);
    try std.testing.expect(changed);
    try std.testing.expectEqualStrings("Primary Workspace", reg.accounts.items[0].account_name.?);
    try std.testing.expect(reg.accounts.items[1].account_name == null);
    try std.testing.expectEqualStrings("Unrelated Workspace", reg.accounts.items[2].account_name.?);
}

test "registry save omits api config" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    reg.api.account = false;

    const rec = try makeAccountRecord(gpa, "a@b.com", "work", .pro, .chatgpt, 1);
    try reg.accounts.append(gpa, rec);
    try registry.saveRegistry(gpa, codex_home, &reg);

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);
    try std.testing.expect(loaded.api.usage);
    try std.testing.expect(loaded.api.account);

    const registry_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "accounts", "registry.json" });
    defer gpa.free(registry_path);
    const saved = try fixtures.readFileAlloc(gpa, registry_path);
    defer gpa.free(saved);
    try std.testing.expect(std.mem.indexOf(u8, saved, "\"api\"") == null);
}

test "registry load ignores legacy api.usage and rewrites file without api config" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");
    try tmp.dir.writeFile(.{
        .sub_path = "accounts/registry.json",
        .data =
        \\{
        \\  "schema_version": 3,
        \\  "active_account_key": null,
        \\  "api": {
        \\    "usage": false
        \\  },
        \\  "accounts": []
        \\}
        ,
    });

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);
    try std.testing.expect(loaded.api.usage);
    try std.testing.expect(loaded.api.account);

    const registry_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "accounts", "registry.json" });
    defer gpa.free(registry_path);
    const saved = try fixtures.readFileAlloc(gpa, registry_path);
    defer gpa.free(saved);
    try std.testing.expect(std.mem.indexOf(u8, saved, "\"api\"") == null);
}

test "registry load ignores legacy api.account and rewrites file without api config" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");
    try tmp.dir.writeFile(.{
        .sub_path = "accounts/registry.json",
        .data =
        \\{
        \\  "schema_version": 3,
        \\  "active_account_key": null,
        \\  "api": {
        \\    "account": false
        \\  },
        \\  "accounts": []
        \\}
        ,
    });

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);
    try std.testing.expect(loaded.api.usage);
    try std.testing.expect(loaded.api.account);

    const registry_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "accounts", "registry.json" });
    defer gpa.free(registry_path);
    const saved = try fixtures.readFileAlloc(gpa, registry_path);
    defer gpa.free(saved);
    try std.testing.expect(std.mem.indexOf(u8, saved, "\"api\"") == null);
}

test "legacy schema registry with legacy rollout attribution rewrites to normalized current schema" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");
    try tmp.dir.writeFile(.{
        .sub_path = "accounts/registry.json",
        .data =
        \\{
        \\  "schema_version": 3,
        \\  "active_account_key": "user-ESYgcy2QkOGZc0NoxSlFCeVT::67fe2bbb-0de6-49a4-b2b3-d1df366d1faf",
        \\  "last_attributed_rollout": {
        \\    "path": "/tmp/sessions/run-1/rollout-a.jsonl",
        \\    "event_timestamp_ms": 1735689600000
        \\  },
        \\  "accounts": [
        \\    {
        \\      "account_key": "user-ESYgcy2QkOGZc0NoxSlFCeVT::67fe2bbb-0de6-49a4-b2b3-d1df366d1faf",
        \\      "chatgpt_account_id": "67fe2bbb-0de6-49a4-b2b3-d1df366d1faf",
        \\      "chatgpt_user_id": "user-ESYgcy2QkOGZc0NoxSlFCeVT",
        \\      "email": "a@b.com",
        \\      "alias": "work",
        \\      "plan": "pro",
        \\      "auth_mode": "chatgpt",
        \\      "created_at": 1,
        \\      "last_used_at": null,
        \\      "last_usage_at": null
        \\    }
        \\  ]
        \\}
        ,
    });

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);
    try std.testing.expectEqual(@as(?i64, 0), loaded.active_account_activated_at_ms);
    try std.testing.expect(loaded.accounts.items[0].last_local_rollout == null);

    var file = try tmp.dir.openFile("accounts/registry.json", .{});
    defer file.close();
    const contents = try file.readToEndAlloc(gpa, 10 * 1024 * 1024);
    defer gpa.free(contents);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"schema_version\": 5") != null);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"active_account_activated_at_ms\": 0") != null);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"last_attributed_rollout\"") == null);
}

test "legacy current-layout registry version field rewrites to schema_version" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");
    try tmp.dir.writeFile(.{
        .sub_path = "accounts/registry.json",
        .data =
        \\{
        \\  "version": 3,
        \\  "active_account_key": null,
        \\  "accounts": []
        \\}
        ,
    });

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);
    try std.testing.expect(loaded.schema_version == registry.current_schema_version);

    var file = try tmp.dir.openFile("accounts/registry.json", .{});
    defer file.close();
    const contents = try file.readToEndAlloc(gpa, 10 * 1024 * 1024);
    defer gpa.free(contents);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"schema_version\": 5") != null);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"interval_seconds\": 60") != null);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"live\"") == null);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"version\"") == null);
}

test "too-new schema version is rejected without rewriting registry" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");
    try tmp.dir.writeFile(.{
        .sub_path = "accounts/registry.json",
        .data =
        \\{
        \\  "schema_version": 999,
        \\  "active_account_key": null,
        \\  "accounts": []
        \\}
        ,
    });

    try std.testing.expectError(error.UnsupportedRegistryVersion, registry.loadRegistry(gpa, codex_home));

    var file = try tmp.dir.openFile("accounts/registry.json", .{});
    defer file.close();
    const contents = try file.readToEndAlloc(gpa, 10 * 1024 * 1024);
    defer gpa.free(contents);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"schema_version\": 999") != null);
}

test "v2 registry migrates active email records to current schema" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    const legacy_auth = try authJsonWithEmailPlan(gpa, "legacy@example.com", "team");
    defer gpa.free(legacy_auth);
    const legacy_snapshot_rel = try legacySnapshotRelPath(gpa, "legacy@example.com");
    defer gpa.free(legacy_snapshot_rel);
    try tmp.dir.writeFile(.{ .sub_path = legacy_snapshot_rel, .data = legacy_auth });

    try tmp.dir.writeFile(.{
        .sub_path = "accounts/registry.json",
        .data =
        \\{
        \\  "version": 2,
        \\  "active_email": "legacy@example.com",
        \\  "accounts": [
        \\    {
        \\      "email": "legacy@example.com",
        \\      "alias": "work",
        \\      "plan": "team",
        \\      "auth_mode": "chatgpt",
        \\      "created_at": 1,
        \\      "last_used_at": null,
        \\      "last_usage_at": null
        \\    }
        \\  ]
        \\}
        ,
    });

    var loaded = try registry.loadRegistry(gpa, codex_home);
    defer loaded.deinit(gpa);
    try std.testing.expect(loaded.schema_version == registry.current_schema_version);
    try std.testing.expect(loaded.accounts.items.len == 1);

    const expected_account_id = try accountKeyForEmailAlloc(gpa, "legacy@example.com");
    defer gpa.free(expected_account_id);
    try std.testing.expect(loaded.active_account_key != null);
    try std.testing.expect(std.mem.eql(u8, loaded.active_account_key.?, expected_account_id));

    const migrated_snapshot_path = try registry.accountAuthPath(gpa, codex_home, expected_account_id);
    defer gpa.free(migrated_snapshot_path);
    var migrated_snapshot = try fs.cwd().openFile(migrated_snapshot_path, .{});
    migrated_snapshot.close();

    var file = try tmp.dir.openFile("accounts/registry.json", .{});
    defer file.close();
    const contents = try file.readToEndAlloc(gpa, 10 * 1024 * 1024);
    defer gpa.free(contents);
    try std.testing.expect(std.mem.indexOf(u8, contents, "\"schema_version\": 5") != null);
    const active_expect = try std.fmt.allocPrint(gpa, "\"active_account_key\": \"{s}\"", .{expected_account_id});
    defer gpa.free(active_expect);
    try std.testing.expect(std.mem.indexOf(u8, contents, active_expect) != null);
}

test "ensureAccountsDir hardens accounts directory without changing codex home permissions" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const tmp_root = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(tmp_root);
    try tmp.dir.makePath("codex-home");

    const codex_home = try fs.path.join(gpa, &[_][]const u8{ tmp_root, "codex-home" });
    defer gpa.free(codex_home);
    try setModeUnix(codex_home, 0o755);

    try registry.ensureAccountsDir(gpa, codex_home);

    const accounts_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "accounts" });
    defer gpa.free(accounts_path);
    try expectModeUnix(codex_home, 0o755);
    try expectModeUnix(accounts_path, 0o700);
}

test "copyManagedFile creates destination with 0600 regardless of source mode" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);

    try tmp.dir.writeFile(.{ .sub_path = "source.json", .data = "secret" });
    const src = try fs.path.join(gpa, &[_][]const u8{ codex_home, "source.json" });
    defer gpa.free(src);
    try setModeUnix(src, 0o644);
    const dest = try fs.path.join(gpa, &[_][]const u8{ codex_home, "dest.json" });
    defer gpa.free(dest);

    try registry.copyManagedFile(src, dest);
    try expectModeUnix(dest, 0o600);
}

test "saveRegistry creates registry.json with 0600 on first write" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    try registry.saveRegistry(gpa, codex_home, &reg);

    const registry_path = try registry.registryPath(gpa, codex_home);
    defer gpa.free(registry_path);
    try expectModeUnix(registry_path, 0o600);
}

test "saveRegistry hardens registry.json to 0600 even when contents are unchanged" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    try registry.saveRegistry(gpa, codex_home, &reg);

    const registry_path = try registry.registryPath(gpa, codex_home);
    defer gpa.free(registry_path);
    try setModeUnix(registry_path, 0o644);

    try registry.saveRegistry(gpa, codex_home, &reg);
    try expectModeUnix(registry_path, 0o600);
}

test "auth backup only on change" {
    var gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);

    const current = try fs.path.join(gpa, &[_][]const u8{ codex_home, "auth.json" });
    defer gpa.free(current);
    const user_account_id = try accountKeyForEmailAlloc(gpa, "user@example.com");
    defer gpa.free(user_account_id);
    const new_auth = try registry.accountAuthPath(gpa, codex_home, user_account_id);
    defer gpa.free(new_auth);
    const account_name = fs.path.basename(new_auth);
    const account_path = try fs.path.join(gpa, &[_][]const u8{ "accounts", account_name });
    defer gpa.free(account_path);

    try tmp.dir.makePath("accounts");
    try tmp.dir.writeFile(.{ .sub_path = "auth.json", .data = "one" });
    try tmp.dir.writeFile(.{ .sub_path = account_path, .data = "two" });

    try registry.backupAuthIfChanged(gpa, codex_home, current, new_auth);

    var accounts = try tmp.dir.openDir("accounts", .{ .iterate = true });
    defer accounts.close();
    const count1 = try countBackups(accounts, "auth.json");
    try std.testing.expect(count1 == 1);
    var verify_accounts = try tmp.dir.openDir("accounts", .{ .iterate = true });
    defer verify_accounts.close();
    var it = verify_accounts.iterate();
    var saw_backup = false;
    while (try it.next()) |entry| {
        if (entry.kind != .file) continue;
        if (std.mem.startsWith(u8, entry.name, "auth.json") and std.mem.containsAtLeast(u8, entry.name, 1, ".bak.")) {
            try expectBackupNameFormat(entry.name, "auth.json");
            const backup_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "accounts", entry.name });
            defer gpa.free(backup_path);
            try expectModeUnix(backup_path, 0o600);
            saw_backup = true;
        }
    }
    try std.testing.expect(saw_backup);

    try tmp.dir.writeFile(.{ .sub_path = "auth.json", .data = "two" });
    try registry.backupAuthIfChanged(gpa, codex_home, current, new_auth);
    const count2 = try countBackups(accounts, "auth.json");
    try std.testing.expect(count2 == 1);
}

test "auth backup rotation" {
    var gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);

    const current = try fs.path.join(gpa, &[_][]const u8{ codex_home, "auth.json" });
    defer gpa.free(current);
    const user_account_id = try accountKeyForEmailAlloc(gpa, "user@example.com");
    defer gpa.free(user_account_id);
    const new_auth = try registry.accountAuthPath(gpa, codex_home, user_account_id);
    defer gpa.free(new_auth);
    const account_name = fs.path.basename(new_auth);
    const account_path = try fs.path.join(gpa, &[_][]const u8{ "accounts", account_name });
    defer gpa.free(account_path);

    try tmp.dir.makePath("accounts");
    try tmp.dir.writeFile(.{ .sub_path = account_path, .data = "base" });

    var i: usize = 0;
    while (i < 8) : (i += 1) {
        const data = try std.fmt.allocPrint(gpa, "v{d}", .{i});
        defer gpa.free(data);
        try tmp.dir.writeFile(.{ .sub_path = "auth.json", .data = data });
        try registry.backupAuthIfChanged(gpa, codex_home, current, new_auth);
    }

    var accounts = try tmp.dir.openDir("accounts", .{ .iterate = true });
    defer accounts.close();
    const count = try countBackups(accounts, "auth.json");
    try std.testing.expect(count <= 5);
}

test "sync active auth leaves auth json permissions unchanged while hardening matching snapshot" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    const rec = try makeAccountRecord(gpa, "user@example.com", "work", .pro, .chatgpt, 1);
    try reg.accounts.append(gpa, rec);
    try registry.setActiveAccountKey(gpa, &reg, reg.accounts.items[0].account_key);

    const active_auth = try authJsonWithEmailPlan(gpa, "user@example.com", "pro");
    defer gpa.free(active_auth);
    try tmp.dir.writeFile(.{ .sub_path = "auth.json", .data = active_auth });

    const account_key = try accountKeyForEmailAlloc(gpa, "user@example.com");
    defer gpa.free(account_key);
    const snapshot_path = try registry.accountAuthPath(gpa, codex_home, account_key);
    defer gpa.free(snapshot_path);
    const snapshot_name = fs.path.basename(snapshot_path);
    const snapshot_rel = try fs.path.join(gpa, &[_][]const u8{ "accounts", snapshot_name });
    defer gpa.free(snapshot_rel);
    try tmp.dir.writeFile(.{ .sub_path = snapshot_rel, .data = active_auth });

    const auth_path = try registry.activeAuthPath(gpa, codex_home);
    defer gpa.free(auth_path);
    try setModeUnix(auth_path, 0o644);
    try setModeUnix(snapshot_path, 0o644);

    const changed = try registry.syncActiveAccountFromAuth(gpa, codex_home, &reg);
    try std.testing.expect(!changed);
    try expectModeUnix(auth_path, 0o644);
    try expectModeUnix(snapshot_path, 0o600);
}

test "replaceActiveAuthWithAccountByKey preserves existing auth json permissions" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    const rec = try makeAccountRecord(gpa, "user@example.com", "work", .pro, .chatgpt, 1);
    try reg.accounts.append(gpa, rec);

    try registry.ensureAccountsDir(gpa, codex_home);

    const active_auth = try authJsonWithEmailPlan(gpa, "other@example.com", "plus");
    defer gpa.free(active_auth);
    try tmp.dir.writeFile(.{ .sub_path = "auth.json", .data = active_auth });

    const account_auth = try authJsonWithEmailPlan(gpa, "user@example.com", "pro");
    defer gpa.free(account_auth);
    const account_key = try accountKeyForEmailAlloc(gpa, "user@example.com");
    defer gpa.free(account_key);
    const snapshot_path = try registry.accountAuthPath(gpa, codex_home, account_key);
    defer gpa.free(snapshot_path);
    const snapshot_rel = try fs.path.join(gpa, &[_][]const u8{ "accounts", fs.path.basename(snapshot_path) });
    defer gpa.free(snapshot_rel);
    try tmp.dir.writeFile(.{ .sub_path = snapshot_rel, .data = account_auth });
    try setModeUnix(snapshot_path, 0o600);

    const auth_path = try registry.activeAuthPath(gpa, codex_home);
    defer gpa.free(auth_path);
    try setModeUnix(auth_path, 0o644);

    try registry.replaceActiveAuthWithAccountByKey(gpa, codex_home, &reg, account_key);

    var auth_file = try fs.cwd().openFile(auth_path, .{});
    defer auth_file.close();
    const auth_bytes = try auth_file.readToEndAlloc(gpa, 1024 * 1024);
    defer gpa.free(auth_bytes);
    try std.testing.expectEqualStrings(account_auth, auth_bytes);
    try expectModeUnix(auth_path, 0o644);
}

test "activateAccountByKey preserves snapshot permissions when auth json is created" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    const rec = try makeAccountRecord(gpa, "user@example.com", "work", .pro, .chatgpt, 1);
    try reg.accounts.append(gpa, rec);

    try registry.ensureAccountsDir(gpa, codex_home);

    const account_auth = try authJsonWithEmailPlan(gpa, "user@example.com", "pro");
    defer gpa.free(account_auth);
    const account_key = try accountKeyForEmailAlloc(gpa, "user@example.com");
    defer gpa.free(account_key);
    const snapshot_path = try registry.accountAuthPath(gpa, codex_home, account_key);
    defer gpa.free(snapshot_path);
    const snapshot_rel = try fs.path.join(gpa, &[_][]const u8{ "accounts", fs.path.basename(snapshot_path) });
    defer gpa.free(snapshot_rel);
    try tmp.dir.writeFile(.{ .sub_path = snapshot_rel, .data = account_auth });
    try setModeUnix(snapshot_path, 0o600);

    const auth_path = try registry.activeAuthPath(gpa, codex_home);
    defer gpa.free(auth_path);
    try std.testing.expectError(error.FileNotFound, fs.cwd().statFile(auth_path));

    try registry.activateAccountByKey(gpa, codex_home, &reg, account_key);

    var auth_file = try fs.cwd().openFile(auth_path, .{});
    defer auth_file.close();
    const auth_bytes = try auth_file.readToEndAlloc(gpa, 1024 * 1024);
    defer gpa.free(auth_bytes);
    try std.testing.expectEqualStrings(account_auth, auth_bytes);
    try expectModeUnix(auth_path, 0o600);
}

test "sync active auth matches by email and updates account auth" {
    var gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    const rec = try makeAccountRecord(gpa, "user@example.com", "work", null, null, 1);
    try reg.accounts.append(gpa, rec);

    const account_auth = try authJsonWithEmailPlan(gpa, "user@example.com", "pro");
    defer gpa.free(account_auth);
    const user_account_id = try accountKeyForEmailAlloc(gpa, "user@example.com");
    defer gpa.free(user_account_id);
    const account_auth_abs = try registry.accountAuthPath(gpa, codex_home, user_account_id);
    defer gpa.free(account_auth_abs);
    const account_name = fs.path.basename(account_auth_abs);
    const account_path = try fs.path.join(gpa, &[_][]const u8{ "accounts", account_name });
    defer gpa.free(account_path);
    try tmp.dir.writeFile(.{ .sub_path = account_path, .data = account_auth });

    const active_auth = try authJsonWithEmailPlan(gpa, "user@example.com", "free");
    defer gpa.free(active_auth);
    try tmp.dir.writeFile(.{ .sub_path = "auth.json", .data = active_auth });

    const changed = try registry.syncActiveAccountFromAuth(gpa, codex_home, &reg);
    try std.testing.expect(changed);
    try std.testing.expect(reg.accounts.items.len == 1);
    try std.testing.expect(std.mem.eql(u8, reg.accounts.items[0].email, "user@example.com"));

    const acc_path = try registry.accountAuthPath(gpa, codex_home, user_account_id);
    defer gpa.free(acc_path);
    var file = try fs.cwd().openFile(acc_path, .{});
    defer file.close();
    const data = try file.readToEndAlloc(gpa, 10 * 1024 * 1024);
    defer gpa.free(data);
    try std.testing.expect(std.mem.eql(u8, data, active_auth));
}

test "sync active auth preserves previous account on external drift" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "previous@example.com", "previous", .plus, .chatgpt, 1));
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "active@example.com", "active", .pro, .chatgpt, 2));
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "external@example.com", "external", .free, .chatgpt, 3));

    const previous_key = try accountKeyForEmailAlloc(gpa, "previous@example.com");
    defer gpa.free(previous_key);
    const active_key = try accountKeyForEmailAlloc(gpa, "active@example.com");
    defer gpa.free(active_key);
    const external_key = try accountKeyForEmailAlloc(gpa, "external@example.com");
    defer gpa.free(external_key);
    try registry.setActiveAccountKey(gpa, &reg, previous_key);
    try registry.setActiveAccountKey(gpa, &reg, active_key);

    const active_auth = try authJsonWithEmailPlan(gpa, "external@example.com", "team");
    defer gpa.free(active_auth);
    try tmp.dir.writeFile(.{ .sub_path = "auth.json", .data = active_auth });

    const changed = try registry.syncActiveAccountFromAuth(gpa, codex_home, &reg);
    try std.testing.expect(changed);
    try std.testing.expect(reg.active_account_key != null);
    try std.testing.expectEqualStrings(external_key, reg.active_account_key.?);
    try std.testing.expect(reg.previous_active_account_key != null);
    try std.testing.expectEqualStrings(previous_key, reg.previous_active_account_key.?);
}

test "registry backup only on change" {
    var gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    try registry.saveRegistry(gpa, codex_home, &reg);

    var accounts = try tmp.dir.openDir("accounts", .{ .iterate = true });
    defer accounts.close();
    const count0 = try countBackups(accounts, "registry.json");
    try std.testing.expect(count0 == 0);

    const rec = try makeAccountRecord(gpa, "user@example.com", "work", null, null, 1);
    try reg.accounts.append(gpa, rec);

    try registry.saveRegistry(gpa, codex_home, &reg);
    const count1 = try countBackups(accounts, "registry.json");
    try std.testing.expect(count1 == 1);
    var verify_accounts = try tmp.dir.openDir("accounts", .{ .iterate = true });
    defer verify_accounts.close();
    var it = verify_accounts.iterate();
    var saw_backup = false;
    while (try it.next()) |entry| {
        if (entry.kind != .file) continue;
        if (std.mem.startsWith(u8, entry.name, "registry.json") and std.mem.containsAtLeast(u8, entry.name, 1, ".bak.")) {
            try expectBackupNameFormat(entry.name, "registry.json");
            const backup_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "accounts", entry.name });
            defer gpa.free(backup_path);
            try expectModeUnix(backup_path, 0o600);
            saw_backup = true;
        }
    }
    try std.testing.expect(saw_backup);

    try registry.saveRegistry(gpa, codex_home, &reg);
    const count2 = try countBackups(accounts, "registry.json");
    try std.testing.expect(count2 == 1);
}

test "clean uses a whitelist and only removes non-current entries under accounts" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    const active_record = try makeAccountRecord(gpa, "keep@example.com", "", .team, .chatgpt, 1);
    try reg.accounts.append(gpa, active_record);
    try registry.saveRegistry(gpa, codex_home, &reg);

    const keep_account_id = try accountKeyForEmailAlloc(gpa, "keep@example.com");
    defer gpa.free(keep_account_id);
    const keep_abs_path = try registry.accountAuthPath(gpa, codex_home, keep_account_id);
    defer gpa.free(keep_abs_path);
    const keep_name = fs.path.basename(keep_abs_path);
    const keep_rel_path = try fs.path.join(gpa, &[_][]const u8{ "accounts", keep_name });
    defer gpa.free(keep_rel_path);

    try tmp.dir.writeFile(.{ .sub_path = "accounts/auth.json.bak.1", .data = "a1" });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/auth.json.bak.2", .data = "a2" });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/auth.json.bak.3", .data = "a3" });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/registry.json.bak.1", .data = "r1" });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/registry.json.bak.2", .data = "r2" });
    try tmp.dir.writeFile(.{ .sub_path = keep_rel_path, .data = "keep" });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/bGVnYWN5QGV4YW1wbGUuY29t.auth.json", .data = "legacy" });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/notes.txt", .data = "junk" });
    try tmp.dir.makePath("accounts/tmpdir");
    try tmp.dir.writeFile(.{ .sub_path = "accounts/tmpdir/old.txt", .data = "junk" });
    try tmp.dir.makePath("accounts/backups/v2/20260312-063235");
    try tmp.dir.writeFile(.{ .sub_path = "accounts/backups/v2/20260312-063235/registry.json", .data = "keep" });

    const summary = try registry.cleanAccountsBackups(gpa, codex_home);
    try std.testing.expect(summary.auth_backups_removed == 3);
    try std.testing.expect(summary.registry_backups_removed == 2);
    try std.testing.expect(summary.stale_snapshot_files_removed == 3);

    var accounts = try tmp.dir.openDir("accounts", .{ .iterate = true });
    defer accounts.close();
    try std.testing.expect(try countBackups(accounts, "auth.json") == 0);
    try std.testing.expect(try countBackups(accounts, "registry.json") == 0);
    var kept = try tmp.dir.openFile(keep_rel_path, .{});
    kept.close();
    try std.testing.expectError(error.FileNotFound, tmp.dir.openFile("accounts/bGVnYWN5QGV4YW1wbGUuY29t.auth.json", .{}));
    try std.testing.expectError(error.FileNotFound, tmp.dir.openFile("accounts/notes.txt", .{}));
    try std.testing.expectError(error.FileNotFound, tmp.dir.openFile("accounts/tmpdir/old.txt", .{}));

    var preserved_backup = try tmp.dir.openFile("accounts/backups/v2/20260312-063235/registry.json", .{});
    preserved_backup.close();
}

test "clean preserves account snapshots when registry is missing" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    const keep_record = try makeAccountRecord(gpa, "keep@example.com", "", .team, .chatgpt, 1);
    try reg.accounts.append(gpa, keep_record);
    try registry.saveRegistry(gpa, codex_home, &reg);

    const keep_account_key = try accountKeyForEmailAlloc(gpa, "keep@example.com");
    defer gpa.free(keep_account_key);
    const keep_abs_path = try registry.accountAuthPath(gpa, codex_home, keep_account_key);
    defer gpa.free(keep_abs_path);
    const keep_rel_path = try fs.path.join(gpa, &[_][]const u8{ "accounts", fs.path.basename(keep_abs_path) });
    defer gpa.free(keep_rel_path);

    const recover_account_key = try accountKeyForEmailAlloc(gpa, "recover@example.com");
    defer gpa.free(recover_account_key);
    const recover_abs_path = try registry.accountAuthPath(gpa, codex_home, recover_account_key);
    defer gpa.free(recover_abs_path);
    const recover_rel_path = try fs.path.join(gpa, &[_][]const u8{ "accounts", fs.path.basename(recover_abs_path) });
    defer gpa.free(recover_rel_path);

    try tmp.dir.writeFile(.{ .sub_path = keep_rel_path, .data = "keep" });
    try tmp.dir.writeFile(.{ .sub_path = recover_rel_path, .data = "recover" });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/auth.json.bak.1", .data = "a1" });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/registry.json.bak.1", .data = "r1" });
    try tmp.dir.deleteFile("accounts/registry.json");

    const summary = try registry.cleanAccountsBackups(gpa, codex_home);
    try std.testing.expect(summary.auth_backups_removed == 1);
    try std.testing.expect(summary.registry_backups_removed == 1);
    try std.testing.expect(summary.stale_snapshot_files_removed == 0);

    var keep_file = try tmp.dir.openFile(keep_rel_path, .{});
    keep_file.close();
    var recover_file = try tmp.dir.openFile(recover_rel_path, .{});
    recover_file.close();
}

test "remove accounts deletes matching snapshots and auth backups only for removed records" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "remove@example.com", "", .plus, .chatgpt, 1));
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "keep@example.com", "", .team, .chatgpt, 2));

    const remove_account_key = try accountKeyForEmailAlloc(gpa, "remove@example.com");
    defer gpa.free(remove_account_key);
    const keep_account_key = try accountKeyForEmailAlloc(gpa, "keep@example.com");
    defer gpa.free(keep_account_key);
    try registry.setActiveAccountKey(gpa, &reg, remove_account_key);

    const remove_snapshot_path = try registry.accountAuthPath(gpa, codex_home, remove_account_key);
    defer gpa.free(remove_snapshot_path);
    const keep_snapshot_path = try registry.accountAuthPath(gpa, codex_home, keep_account_key);
    defer gpa.free(keep_snapshot_path);

    const remove_auth = try authJsonWithEmailPlan(gpa, "remove@example.com", "plus");
    defer gpa.free(remove_auth);
    const keep_auth = try authJsonWithEmailPlan(gpa, "keep@example.com", "team");
    defer gpa.free(keep_auth);

    try fs.cwd().writeFile(.{ .sub_path = remove_snapshot_path, .data = remove_auth });
    try fs.cwd().writeFile(.{ .sub_path = keep_snapshot_path, .data = keep_auth });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/auth.json.bak.20260320-010101", .data = remove_auth });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/auth.json.bak.20260320-020202", .data = keep_auth });
    try tmp.dir.writeFile(.{ .sub_path = "accounts/auth.json.bak.20260320-030303", .data = "{not-json}" });

    try registry.removeAccounts(gpa, codex_home, &reg, &[_]usize{0});

    try std.testing.expectEqual(@as(usize, 1), reg.accounts.items.len);
    try std.testing.expect(std.mem.eql(u8, reg.accounts.items[0].email, "keep@example.com"));
    try std.testing.expect(reg.active_account_key == null);

    try std.testing.expectError(error.FileNotFound, fs.cwd().openFile(remove_snapshot_path, .{}));
    var keep_snapshot = try fs.cwd().openFile(keep_snapshot_path, .{});
    keep_snapshot.close();

    try std.testing.expectError(error.FileNotFound, tmp.dir.openFile("accounts/auth.json.bak.20260320-010101", .{}));
    var keep_backup = try tmp.dir.openFile("accounts/auth.json.bak.20260320-020202", .{});
    keep_backup.close();
    var malformed_backup = try tmp.dir.openFile("accounts/auth.json.bak.20260320-030303", .{});
    malformed_backup.close();
}

test "remove accounts clears previous active account when previous is removed" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("accounts");

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "previous@example.com", "", .plus, .chatgpt, 1));
    try reg.accounts.append(gpa, try makeAccountRecord(gpa, "active@example.com", "", .team, .chatgpt, 2));

    const previous_key = try accountKeyForEmailAlloc(gpa, "previous@example.com");
    defer gpa.free(previous_key);
    const active_key = try accountKeyForEmailAlloc(gpa, "active@example.com");
    defer gpa.free(active_key);
    try registry.setActiveAccountKey(gpa, &reg, previous_key);
    try registry.setActiveAccountKey(gpa, &reg, active_key);

    try registry.removeAccounts(gpa, codex_home, &reg, &[_]usize{0});

    try std.testing.expect(reg.active_account_key != null);
    try std.testing.expectEqualStrings(active_key, reg.active_account_key.?);
    try std.testing.expect(reg.previous_active_account_key == null);
}

test "import auth path with single file keeps explicit alias" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const auth_json = try authJsonWithEmailPlan(gpa, "single@example.com", "plus");
    defer gpa.free(auth_json);
    try tmp.dir.writeFile(.{ .sub_path = "imports/one.json", .data = auth_json });

    const one_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports", "one.json" });
    defer gpa.free(one_path);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var summary = try registry.importAuthPath(gpa, codex_home, &reg, one_path, "personal");
    defer summary.deinit(gpa);
    try std.testing.expect(summary.render_kind == .single_file);
    try std.testing.expect(summary.imported == 1);
    try std.testing.expect(summary.updated == 0);
    try std.testing.expect(summary.skipped == 0);
    try std.testing.expect(summary.total_files == 1);
    try std.testing.expect(reg.accounts.items.len == 1);
    try std.testing.expect(std.mem.eql(u8, reg.accounts.items[0].alias, "personal"));
}

test "import auth path with json array imports each top-level item" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const first_auth = try authJsonWithEmailPlan(gpa, "array-one@example.com", "plus");
    defer gpa.free(first_auth);
    const second_auth = try authJsonWithEmailPlan(gpa, "array-two@example.com", "team");
    defer gpa.free(second_auth);
    const array_auth = try std.fmt.allocPrint(gpa, "[{s},{s}]", .{ first_auth, second_auth });
    defer gpa.free(array_auth);
    try tmp.dir.writeFile(.{ .sub_path = "imports/token_array.json", .data = array_auth });

    const import_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports", "token_array.json" });
    defer gpa.free(import_path);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var summary = try registry.importAuthPath(gpa, codex_home, &reg, import_path, null);
    defer summary.deinit(gpa);

    try std.testing.expect(summary.render_kind == .single_file);
    try std.testing.expect(summary.imported == 2);
    try std.testing.expect(summary.updated == 0);
    try std.testing.expect(summary.skipped == 0);
    try std.testing.expect(summary.total_files == 1);
    try std.testing.expectEqual(@as(usize, 2), summary.events.items.len);
    try std.testing.expectEqualStrings("token_array.json", summary.events.items[0].label);
    try std.testing.expectEqualStrings("token_array.json", summary.events.items[1].label);
    try std.testing.expectEqual(@as(?usize, 1), summary.events.items[0].item_index);
    try std.testing.expectEqual(@as(?usize, 2), summary.events.items[1].item_index);
    try std.testing.expectEqualStrings("array-one@example.com", summary.events.items[0].detail.?);
    try std.testing.expectEqualStrings("array-two@example.com", summary.events.items[1].detail.?);
    try std.testing.expect(reg.accounts.items.len == 2);
}

test "import auth path with malformed single file reports skipped" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");
    try tmp.dir.writeFile(.{ .sub_path = "imports/bad.json", .data = "{not-json}" });

    const import_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports", "bad.json" });
    defer gpa.free(import_path);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var summary = try registry.importAuthPath(gpa, codex_home, &reg, import_path, null);
    defer summary.deinit(gpa);

    try std.testing.expect(summary.render_kind == .single_file);
    try std.testing.expect(summary.imported == 0);
    try std.testing.expect(summary.updated == 0);
    try std.testing.expect(summary.skipped == 1);
    try std.testing.expect(summary.failure != null);
    try std.testing.expectEqual(error.SyntaxError, summary.failure.?);
    try std.testing.expectEqualStrings("bad.json", summary.events.items[0].label);
    try std.testing.expectEqualStrings("InvalidJSON", summary.events.items[0].reason.?);
}

test "import reason labels override only public names that differ from internal errors" {
    try std.testing.expectEqualStrings("InvalidJSON", registry.importReasonLabel(error.SyntaxError));
    try std.testing.expectEqualStrings("InvalidCPAFormat", registry.importReasonLabel(error.InvalidCPAFormat));
    try std.testing.expectEqualStrings("MaxFileSizeExceeded", registry.importReasonLabel(error.StreamTooLong));
}

test "import auth path with directory imports multiple json files and skips bad files" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const a = try authJsonWithEmailPlan(gpa, "a@example.com", "pro");
    defer gpa.free(a);
    const b = try authJsonWithEmailPlan(gpa, "b@example.com", "team");
    defer gpa.free(b);
    try tmp.dir.writeFile(.{ .sub_path = "imports/a.json", .data = a });
    try tmp.dir.writeFile(.{ .sub_path = "imports/b.json", .data = b });
    try tmp.dir.writeFile(.{ .sub_path = "imports/readme.txt", .data = "ignored" });
    try tmp.dir.writeFile(.{ .sub_path = "imports/bad.json", .data = "{not-json}" });

    const imports_dir = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports" });
    defer gpa.free(imports_dir);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var summary = try registry.importAuthPath(gpa, codex_home, &reg, imports_dir, null);
    defer summary.deinit(gpa);
    try std.testing.expect(summary.render_kind == .scanned);
    try std.testing.expect(summary.imported == 2);
    try std.testing.expect(summary.updated == 0);
    try std.testing.expect(summary.skipped == 1);
    try std.testing.expect(summary.total_files == 3);
    try std.testing.expect(reg.accounts.items.len == 2);
    try std.testing.expect(reg.accounts.items[0].alias.len == 0);
    try std.testing.expect(reg.accounts.items[1].alias.len == 0);

    const account_id_a = try accountKeyForEmailAlloc(gpa, "a@example.com");
    defer gpa.free(account_id_a);
    const path_a = try registry.accountAuthPath(gpa, codex_home, account_id_a);
    defer gpa.free(path_a);
    const account_id_b = try accountKeyForEmailAlloc(gpa, "b@example.com");
    defer gpa.free(account_id_b);
    const path_b = try registry.accountAuthPath(gpa, codex_home, account_id_b);
    defer gpa.free(path_b);
    var file_a = try fs.cwd().openFile(path_a, .{});
    defer file_a.close();
    var file_b = try fs.cwd().openFile(path_b, .{});
    defer file_b.close();
}

test "import auth path with repeated single file reports updated on second import" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const auth_json = try authJsonWithEmailPlan(gpa, "repeat@example.com", "plus");
    defer gpa.free(auth_json);
    try tmp.dir.writeFile(.{ .sub_path = "imports/repeat.json", .data = auth_json });

    const auth_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports", "repeat.json" });
    defer gpa.free(auth_path);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var first = try registry.importAuthPath(gpa, codex_home, &reg, auth_path, null);
    defer first.deinit(gpa);
    try std.testing.expect(first.imported == 1);
    try std.testing.expect(first.updated == 0);
    try std.testing.expect(first.skipped == 0);

    var second = try registry.importAuthPath(gpa, codex_home, &reg, auth_path, null);
    defer second.deinit(gpa);
    try std.testing.expect(second.imported == 0);
    try std.testing.expect(second.updated == 1);
    try std.testing.expect(second.skipped == 0);
    try std.testing.expectEqual(@as(usize, 1), second.events.items.len);
    try std.testing.expect(second.events.items[0].outcome == .updated);
}

test "import auth path with invalid single file keeps failure for non-zero exit handling" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const invalid_auth = try fixtures.authJsonWithoutEmail(gpa);
    defer gpa.free(invalid_auth);
    try tmp.dir.writeFile(.{ .sub_path = "imports/invalid.json", .data = invalid_auth });

    const auth_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports", "invalid.json" });
    defer gpa.free(auth_path);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var report = try registry.importAuthPath(gpa, codex_home, &reg, auth_path, null);
    defer report.deinit(gpa);
    try std.testing.expect(report.render_kind == .single_file);
    try std.testing.expectEqual(@as(usize, 0), report.appliedCount());
    try std.testing.expectEqual(@as(usize, 1), report.skipped);
    const failure = report.failure orelse return error.TestExpectedEqual;
    try std.testing.expect(failure == error.MissingEmail);
    try std.testing.expectEqual(@as(usize, 1), report.events.items.len);
    try std.testing.expect(report.events.items[0].outcome == .skipped);
    try std.testing.expectEqualStrings("MissingEmail", report.events.items[0].reason.?);
}

test "import cpa path with single file converts to standard auth and keeps explicit alias" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const cpa_json = try fixtures.cpaJsonWithEmailPlan(gpa, "single-cpa@example.com", "plus");
    defer gpa.free(cpa_json);
    try tmp.dir.writeFile(.{ .sub_path = "imports/one.json", .data = cpa_json });

    const one_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports", "one.json" });
    defer gpa.free(one_path);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var report = try registry.importCpaPath(gpa, codex_home, &reg, one_path, "personal");
    defer report.deinit(gpa);
    try std.testing.expect(report.render_kind == .single_file);
    try std.testing.expect(report.imported == 1);
    try std.testing.expectEqual(@as(usize, 1), reg.accounts.items.len);
    try std.testing.expect(std.mem.eql(u8, reg.accounts.items[0].alias, "personal"));

    const account_key = try fixtures.accountKeyForEmailAlloc(gpa, "single-cpa@example.com");
    defer gpa.free(account_key);
    const snapshot_path = try registry.accountAuthPath(gpa, codex_home, account_key);
    defer gpa.free(snapshot_path);
    const snapshot_data = try fixtures.readFileAlloc(gpa, snapshot_path);
    defer gpa.free(snapshot_data);
    try std.testing.expect(std.mem.indexOf(u8, snapshot_data, "\"tokens\": {") != null);
    try std.testing.expect(std.mem.indexOf(u8, snapshot_data, "\"refresh_token\": \"refresh-single-cpa@example.com\"") != null);
    try expectModeUnix(snapshot_path, 0o600);
}

test "import cpa path with empty last refresh omits refresh metadata" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const cpa_json = try fixtures.cpaJsonWithEmailPlan(gpa, "empty-cpa-refresh@example.com", "plus");
    defer gpa.free(cpa_json);
    const empty_last_refresh = try std.mem.replaceOwned(
        u8,
        gpa,
        cpa_json,
        "\"last_refresh\":\"2026-03-20T00:00:00Z\"",
        "\"last_refresh\":\"\"",
    );
    defer gpa.free(empty_last_refresh);
    try tmp.dir.writeFile(.{ .sub_path = "imports/empty-refresh.json", .data = empty_last_refresh });

    const import_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports", "empty-refresh.json" });
    defer gpa.free(import_path);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var report = try registry.importCpaPath(gpa, codex_home, &reg, import_path, null);
    defer report.deinit(gpa);
    try std.testing.expect(report.imported == 1);

    const account_key = try fixtures.accountKeyForEmailAlloc(gpa, "empty-cpa-refresh@example.com");
    defer gpa.free(account_key);
    const snapshot_path = try registry.accountAuthPath(gpa, codex_home, account_key);
    defer gpa.free(snapshot_path);
    const snapshot_data = try fixtures.readFileAlloc(gpa, snapshot_path);
    defer gpa.free(snapshot_data);
    try std.testing.expect(std.mem.indexOf(u8, snapshot_data, "\"last_refresh\"") == null);
}

test "import cpa path with repeated single file reports updated on second import" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const cpa_json = try fixtures.cpaJsonWithEmailPlan(gpa, "repeat-cpa@example.com", "pro");
    defer gpa.free(cpa_json);
    try tmp.dir.writeFile(.{ .sub_path = "imports/repeat.json", .data = cpa_json });

    const import_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports", "repeat.json" });
    defer gpa.free(import_path);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var first = try registry.importCpaPath(gpa, codex_home, &reg, import_path, null);
    defer first.deinit(gpa);
    try std.testing.expect(first.imported == 1);
    try std.testing.expect(first.updated == 0);
    try std.testing.expect(first.events.items[0].outcome == .imported);

    var second = try registry.importCpaPath(gpa, codex_home, &reg, import_path, null);
    defer second.deinit(gpa);
    try std.testing.expect(second.imported == 0);
    try std.testing.expect(second.updated == 1);
    try std.testing.expect(second.events.items[0].outcome == .updated);
}

test "import cpa path with directory imports multiple json files and skips bad files" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const a = try fixtures.cpaJsonWithEmailPlan(gpa, "a-cpa@example.com", "pro");
    defer gpa.free(a);
    const b = try fixtures.cpaJsonWithEmailPlan(gpa, "b-cpa@example.com", "team");
    defer gpa.free(b);
    const no_refresh = try fixtures.cpaJsonWithoutRefreshToken(gpa, "no-refresh@example.com", "plus");
    defer gpa.free(no_refresh);
    try tmp.dir.writeFile(.{ .sub_path = "imports/a.json", .data = a });
    try tmp.dir.writeFile(.{ .sub_path = "imports/b.json", .data = b });
    try tmp.dir.writeFile(.{ .sub_path = "imports/no-refresh.json", .data = no_refresh });
    try tmp.dir.writeFile(.{ .sub_path = "imports/bad.json", .data = "{not-json}" });
    try tmp.dir.writeFile(.{ .sub_path = "imports/readme.txt", .data = "ignored" });

    const imports_dir = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports" });
    defer gpa.free(imports_dir);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);

    var report = try registry.importCpaPath(gpa, codex_home, &reg, imports_dir, null);
    defer report.deinit(gpa);
    try std.testing.expect(report.render_kind == .scanned);
    try std.testing.expect(report.imported == 3);
    try std.testing.expect(report.updated == 0);
    try std.testing.expect(report.skipped == 1);
    try std.testing.expect(report.total_files == 4);
    try std.testing.expectEqual(@as(usize, 3), reg.accounts.items.len);
}

test "export accounts writes standard auth snapshots to explicit directory" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const cpa_json = try fixtures.cpaJsonWithEmailPlan(gpa, "export-standard@example.com", "plus");
    defer gpa.free(cpa_json);
    try tmp.dir.writeFile(.{ .sub_path = "imports/source.json", .data = cpa_json });

    const import_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports", "source.json" });
    defer gpa.free(import_path);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    var import_report = try registry.importCpaPath(gpa, codex_home, &reg, import_path, null);
    defer import_report.deinit(gpa);

    const export_dir = try fs.path.join(gpa, &[_][]const u8{ codex_home, "exports" });
    defer gpa.free(export_dir);
    var summary = try registry.exportAccounts(gpa, codex_home, &reg, export_dir, .standard);
    defer summary.deinit(gpa);

    try std.testing.expectEqual(@as(usize, 1), summary.exported);
    const account_key = try fixtures.accountKeyForEmailAlloc(gpa, "export-standard@example.com");
    defer gpa.free(account_key);
    const snapshot_name = try registry.accountSnapshotFileName(gpa, account_key);
    defer gpa.free(snapshot_name);
    const exported_path = try fs.path.join(gpa, &[_][]const u8{ export_dir, snapshot_name });
    defer gpa.free(exported_path);
    const exported = try fixtures.readFileAlloc(gpa, exported_path);
    defer gpa.free(exported);
    try std.testing.expect(std.mem.indexOf(u8, exported, "\"tokens\": {") != null);
    try std.testing.expect(std.mem.indexOf(u8, exported, "\"refresh_token\": \"refresh-export-standard@example.com\"") != null);
}

test "export accounts writes cpa token files to default backup directory" {
    const gpa = std.testing.allocator;
    var tmp = fs.tmpDir(.{});
    defer tmp.cleanup();

    const codex_home = try tmp.dir.realpathAlloc(gpa, ".");
    defer gpa.free(codex_home);
    try tmp.dir.makePath("imports");

    const cpa_json = try fixtures.cpaJsonWithEmailPlan(gpa, "export-cpa@example.com", "pro");
    defer gpa.free(cpa_json);
    try tmp.dir.writeFile(.{ .sub_path = "imports/source.json", .data = cpa_json });

    const import_path = try fs.path.join(gpa, &[_][]const u8{ codex_home, "imports", "source.json" });
    defer gpa.free(import_path);

    var reg = makeEmptyRegistry();
    defer reg.deinit(gpa);
    var import_report = try registry.importCpaPath(gpa, codex_home, &reg, import_path, null);
    defer import_report.deinit(gpa);

    var summary = try registry.exportAccounts(gpa, codex_home, &reg, null, .cpa);
    defer summary.deinit(gpa);

    const default_dir = try registry.defaultExportDirectory(gpa, codex_home);
    defer gpa.free(default_dir);
    try std.testing.expectEqualStrings(default_dir, summary.dest_path);
    try std.testing.expectEqual(@as(usize, 1), summary.exported);

    const account_key = try fixtures.accountKeyForEmailAlloc(gpa, "export-cpa@example.com");
    defer gpa.free(account_key);
    const snapshot_name = try registry.accountSnapshotFileName(gpa, account_key);
    defer gpa.free(snapshot_name);
    const cpa_name = try std.mem.concat(gpa, u8, &[_][]const u8{ snapshot_name[0 .. snapshot_name.len - ".auth.json".len], ".json" });
    defer gpa.free(cpa_name);
    const exported_path = try fs.path.join(gpa, &[_][]const u8{ default_dir, cpa_name });
    defer gpa.free(exported_path);
    const exported = try fixtures.readFileAlloc(gpa, exported_path);
    defer gpa.free(exported);
    try std.testing.expect(std.mem.indexOf(u8, exported, "\"refresh_token\": \"refresh-export-cpa@example.com\"") != null);
    try std.testing.expect(std.mem.indexOf(u8, exported, "\"tokens\"") == null);
}
